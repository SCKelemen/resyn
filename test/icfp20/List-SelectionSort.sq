data List a <q :: Int> where
  Nil  :: List a <q>
  Cons :: a -> List {a | | q} <q> -> List a <q>
 
min :: x:a -> xs: List {a | | 1} <{0}> -[1]-> a
min = \x. \xs. 
  match xs with
    Nil       -> x
    Cons y ys -> 
      if y < x 
        then min y ys 
        else min x ys

remove :: x: a -> xs: List {a| |1} <{0}> -[1]-> List a <{0}>
remove = \x. \xs.
  match xs with
    Nil       -> Nil
    Cons y ys -> 
      if x == y 
        then ys
        else Cons y (remove x ys)

selectionSort :: List {a | | 5} <{5}> -[1]-> List a <{0}> 
selectionSort = \xs. 
  match xs with
    Nil       -> Nil
    Cons y ys ->
      let m = min y ys in 
      Cons (m) (selectionSort (remove m xs))
